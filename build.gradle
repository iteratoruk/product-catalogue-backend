import org.springframework.cloud.contract.verifier.config.TestFramework

plugins {
	id 'org.springframework.boot' version '2.2.5.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'org.springframework.cloud.contract' version '2.2.2.RELEASE'
	id 'com.diffplug.gradle.spotless' version '3.27.2'
	id 'com.adarshr.test-logger' version '2.0.0'
	id 'java'
}

group = 'iteratoruk'
version = '0.1.0-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	jcenter()
}

ext {
	set('springCloudVersion', "Hoxton.SR3")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.data:spring-data-rest-hal-browser'
	implementation 'org.flywaydb:flyway-core'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
	testImplementation 'io.rest-assured:rest-assured-all:4.2.0'
	testImplementation 'io.rest-assured:spring-mock-mvc:4.2.0'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

contracts {
	testFramework = TestFramework.JUNIT5
//	failOnNoContracts = false
	packageWithBaseClasses "iteratoruk.product.catalogue.contracts"
	baseClassMappings {
		baseClassMapping(".*", "iteratoruk.product.catalogue.contracts.PrePopulatedDatabaseContractTest")
	}
}

test {
	useJUnitPlatform {}
}

spotless {
	java {
		googleJavaFormat()
		licenseHeaderFile rootProject.file('src/spotless/apache-2.0-licence-header.txt')
		importOrder 'java', 'javax', 'com', 'org'
		trimTrailingWhitespace()
		targetExclude 'build/generated-test-sources/**'
	}
	sql {}
}
